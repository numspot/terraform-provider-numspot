---
page_title: "Provider: Numspot"
---

# Numspot Provider

The Numspot provider is used to provision and manage Numspot infrastructure resources.
The provider needs to be configured with the proper credentials before it can be used.

Use the navigation to the left to read about the available resources.

## Example

Here is an example that will spin-up 2 VMs running behind an internet facing load balancer.

This scenario provisions the following resources :

VPC, Subnet, Internet Gateway, Route table, Security Group, VM, LBU (internet facing)


You can test this config by creating a `test.tf` and run terraform commands from this directory:

- Get your [Numspot credentials](https://console.eu-west-2.numspot.com/fr/iam/service-accounts)
- Get your [Space ID](https://console.eu-west-2.numspot.com/fr/iam/spaces)
- Initialize a Terraform working directory: `terraform init`
- Generate and show the execution plan: `terraform plan`
- Build the infrastructure: `terraform apply`

```hcl
terraform {
  required_providers {
    numspot = {
      source  = "numspot/numspot"
    }
  }
}

provider "numspot" {
  numspot_host  = "https://api.eu-west-2.numspot.com"
  numspot_host_os  = "https://objectstorage.eu-west-2.numspot.com"
  client_id     = "my-svc-account-id"
  client_secret = "my-svc-account-secret"
  space_id      = "my-space-id"
}

resource "numspot_vpc" "vpc" {
  ip_range = "10.0.0.0/16"

  tags = [{
    key   = "name"
    value = "terraform-demo"
  }]
}

resource "numspot_subnet" "subnet" {
  vpc_id   = numspot_vpc.vpc.id
  ip_range = "10.0.0.0/16"

  tags = [{
    key   = "name"
    value = "terraform-demo"
  }]
}

resource "numspot_vm" "vm-1" {
  image_id           = "ami-0ac6a040"
  type               = "ns-cus6-2c4r"
  subnet_id          = numspot_subnet.subnet.id
  security_group_ids = [numspot_security_group.sg.id]
  tags = [{
    key   = "name"
    value = "terraform-demo"
  }]
}

resource "numspot_vm" "vm-2" {
  image_id           = "ami-0ac6a040"
  type               = "ns-cus6-2c4r"
  subnet_id          = numspot_subnet.subnet.id
  security_group_ids = [numspot_security_group.sg.id]
  tags = [{
    key   = "name"
    value = "terraform-demo"
  }]
}

resource "numspot_internet_gateway" "igw" {
  vpc_id = numspot_vpc.vpc.id

  tags = [{
    key   = "name"
    value = "terraform-demo"
  }]
}

resource "numspot_route_table" "rt" {
  vpc_id    = numspot_vpc.vpc.id
  subnet_id = numspot_subnet.subnet.id

  routes = [{
    destination_ip_range = "0.0.0.0/0"
    gateway_id           = numspot_internet_gateway.igw.id
  }]

  tags = [{
    key   = "name"
    value = "terraform-demo"
  }]
}

resource "numspot_security_group" "sg" {
  vpc_id      = numspot_vpc.vpc.id
  name        = "security group terraform demo"
  description = "security group terraform demo description"

  inbound_rules = [{
    from_port_range = 3000
    to_port_range   = 3000
    ip_protocol     = "tcp"
    ip_ranges       = ["0.0.0.0/0"]
  }]

  outbound_rules = [{
    from_port_range = -1
    to_port_range   = -1
    ip_protocol     = "Any"
    ip_ranges       = ["0.0.0.0/0"]
  }]

  tags = [{
    key   = "name"
    value = "terraform-demo"
  }]
}

resource "numspot_load_balancer" "lbu" {
  name = "elb-terraform-demo"
  listeners = [{
    load_balancer_port     = 3000
    backend_port           = 3000
    load_balancer_protocol = "TCP"
  }]

  subnets         = [numspot_subnet.subnet.id]
  security_groups = [numspot_security_group.sg.id]
  backend_vm_ids  = [numspot_vm.vm-1.id, numspot_vm.vm-2.id]

  type = "internet-facing"

  health_check = {
    healthy_threshold   = 10
    check_interval      = 30
    path                = "/"
    port                = 3000
    protocol            = "HTTP"
    timeout             = 5
    unhealthy_threshold = 5
  }

  tags = [{
    key   = "name"
    value = "terraform-demo"
  }]
}
```

<!-- schema generated by tfplugindocs -->
## Schema

### Optional

- `client_id` (String) Client ID to authenticate user.
- `client_secret` (String) Client secret to authenticate user.
- `numspot_host` (String) Numspot API Host
- `numspot_host_os` (String) Numspot API Host of object storage
- `space_id` (String) Space ID.

## Authentication

In order to authenticate to the Numspot terraform provider you need to provider a service account ID, service account secret and a space ID.

The Numspot provider offers two ways of providing these credentials.
The following methods are supported, in this priority order:

1. [Environment variables](#environment-variables)
2. [Static credentials](#static-credentials)


### Environment variables

You can provide your arguments/credentials via the environment variables.

Example:

```hcl
provider "numspot" {}
```

Usage:

```bash
$ export NUMSPOT_HOST="https://api.eu-west-2.numspot.com"
$ export NUMSPOT_HOST_OS="https://objectstorage.eu-west-2.numspot.com"
$ export NUMSPOT_CLIENT_ID="my-svc-account-id"
$ export NUMSPOT_CLIENT_SECRET="my-svc-account-secret"
$ export NUMSPOT_SPACE_ID="my-space-id"
$ terraform plan
```

### Static credentials

~> **Warning**: Avoid hard-coding credentials in Terraform configurations to prevent exposing secrets if the file is ever committed to public version control.

Static credentials can be provided by adding provider specific arguments/credentials in-line in the Numspot provider block:

Example:

```hcl
provider "numspot" {
  numspot_host  = "https://api.eu-west-2.numspot.com"
  numspot_host_os  = "https://objectstorage.eu-west-2.numspot.com/"
  client_id     = "my-svc-account-id"
  client_secret = "my-svc-account-secret"
  space_id      = "my-space-id"
}
```

Since **Terraform** v0.14.0 [input variables](https://developer.hashicorp.com/terraform/language/values/variables) feature was introduced that enables the user to type values on runtime through STD input.

This feature could be used to init terraform provider without hardcoding credentials in Terraform config file.

Example:
```hcl
variable "numspot_host" {
  type    = string
  default = "https://api.eu-west-2.numspot.com"
}

variable "numspot_host_os" {
  type    = string
  default = "https://objectstorage.eu-west-2.numspot.com"
}

variable "client_id" {
  type = string
}

variable "client_secret" {
  type      = string
  sensitive = true
}

variable "space_id" {
  type = string
}

provider "numspot" {
  numspot_host  = var.numspot_host
  numspot_host  = var.numspot_host_os
  client_id     = var.client_id
  client_secret = var.client_secret
  space_id      = var.space_id
}
```

## Arguments Reference

In addition to [generic provider arguments](https://www.terraform.io/docs/configuration/providers.html) (e.g. `alias` and `version`), the following arguments are supported in the Numspot provider block:

| Provider Argument | [Environment Variables](#environment-variables) | Description                                                                             |
|-------------------|-------------------------------------------------|-----------------------------------------------------------------------------------------|
| `numspot_host`    | `NUMSPOT_HOST`                                  | [Numspot API host](https://api.eu-west-2.numspot.com/)                                  |
| `numspot_host_os` | `NUMSPOT_HOST_OS`                               | [Numspot API host OS](https://objectstorage.eu-west-2.numspot.com/)                     |
| `client_id`       | `NUMSPOT_CLIENT_ID`                             | [Service account ID](https://console.eu-west-2.numspot.com/fr/iam/service-accounts)     |
| `client_secret`   | `NUMSPOT_CLIENT_SECRET`                         | [Service account secret](https://console.eu-west-2.numspot.com/fr/iam/service-accounts) |
| `space_id`        | `NUMSPOT_SPACE_ID`                              | [Space ID](https://console.eu-west-2.numspot.com/fr/iam/spaces)                         |


## Debugging

In order to be able to [debug](https://developer.hashicorp.com/terraform/internals/debugging) a deployment, think about Changing log level, e.g:

`TF_LOG=DEBUG TF_LOG_PROVIDER=DEBUG terraform apply`

- `TF_LOG`: All loggers (Terraform, SDKs, providers).
- `TF_LOG_PROVIDER`: All providers and provider SDKs used during the run.

### Submitting a bug report or a feature request

In case you find something wrong with the numspot provider, please submit a bug report on the [Terraform provider repository](https://github.com/numspot/terraform-provider-numspot/issues/new/choose).
If it is a bug report, please include a **minimal** snippet of the Terraform configuration that triggered the error.
This helps a lot to debug the issue.